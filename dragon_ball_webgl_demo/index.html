<html>

<head>
    <title>Dragon Ball WebGL</title>
    <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

    <!-- Llibreria amb operacions amb vectors i matrius -->
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/gl-matrix-min.js"></script>

    <!-- Llibreria que facilita la creació del context WebGL -->
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/webgl-utils.js"></script>

    <!-- Llibreria per importar objectes 3d - http://k3d.ivank.net -->
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/K3D.js"></script>

    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/main.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/Goku.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/interaction.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/programs.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/Escenari.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/bola.js"></script>
    <script type="text/javascript" src="/assets/dragon_ball_webgl/js/Piccolo.js"></script>



    <style>

        body {
            color: #fff;
            font-family: Arial;
            font-size: 13px;
            text-align: center;

            background-color: #000;
            margin: 0px;
            margin-top: 5px;
            top:0px;
            position:absolute;
            width:100%;
            height:100%;
            margin: 0px;
            overflow: hidden;
        }

        canvas {
            width: 100%;
            height: 100%;
        }

        #grup5{
            color: #ffffff;
            position: absolute;
            top: 0px;
            width: 100%;
            padding: 5px;
            font-weight: bold;
        }
        #controls{
            color: #ffffff;
            position: absolute;
            top: 10px;
            right:30px;
            padding: 5px;
            background: rgba(0, 0, 50,0.5);


        }


    </style>





    <!-- Codi WebGL -->


</head>


<div id="grup5"><strong>Grup 5</strong>&nbsp;&nbsp;&nbsp;&nbsp; ls27425_ls23839_ls24044</div>
<div id="controls">
    <strong>Controls</strong></br>
    W--Goku Up</br>
    S--Goku Down</br>
    A--Goku Right</br>
    D--Goku Left</br>
    Q--Goku far</br>
    E--Goku close</br>

    SPACE--Piccolo Attack
</div>
<!-- HTML de la pàgina -->

<body onload="webGLStart();">
<div id = "container">

</div>

<canvas id="webgl-canvas" style="border: none;" width="800" height="500"></canvas>


<div>
    <script id="shader-fs-blinn-text" type="x-shader/x-fragment">
        precision mediump float;
        
        uniform float uLightIntensity;
        uniform vec3 uLightPos;
        uniform vec3 uAmbient;
        uniform vec3 uDiffuse;
        uniform vec3 uSpecular;
        uniform float uSpecularExp;
        uniform sampler2D uSamplerDiff; 
        uniform sampler2D uSamplerSpec; 
        
        varying vec3 vNormal;
        varying vec3 vPosition;
        varying vec2 vTextureCoord;

        void main(void) {

            const float PI = 3.14159265358979323846264;
        
            vec3 light = uLightPos - vPosition;
            float distance = length(light);
            light = normalize(light);
            
            vec3 normal = normalize(vNormal);
            vec3 view = normalize(-vPosition);
            vec3 halfV = normalize(view+light);
                
            float NL = max(0.0, dot(normal, light));
            float NH = max(0.0, dot(normal, halfV));
            float NHexp = pow(NH, uSpecularExp);
                    
            float attenuation = 1.0 / ( 4.0 * PI * distance );
            float lightI = uLightIntensity * attenuation;
            
            vec3 diffColor = texture2D(uSamplerDiff, vTextureCoord).rgb;
            float specIntensity = texture2D(uSamplerSpec, vTextureCoord).r;
            
            vec3 color = (uAmbient + uDiffuse * NL * lightI) * diffColor + uSpecular * NHexp * lightI * specIntensity;
            
            gl_FragColor = vec4(color, 1.0);
        }
    </script>

    <script id="shader-vs-blinn-text" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;
        attribute vec2 aTextureCoord;
        
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;
        uniform mat3 uNMatrix;
        
        varying vec3 vPosition;
        varying vec3 vNormal;
        varying vec2 vTextureCoord;

        void main(void) {
        
            vNormal = uNMatrix * aVertexNormal;
            vPosition = vec3(uMVMatrix * vec4(aVertexPosition, 1.0));

            vTextureCoord = aTextureCoord;
            
            gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        }
    </script>

    <script id="shader-fs-blinn" type="x-shader/x-fragment">
        precision mediump float;
        
        uniform float uLightIntensity;
        uniform vec3 uLightPos;
        uniform vec3 uAmbient;
        uniform vec3 uDiffuse;
        uniform vec3 uSpecular;
        uniform float uSpecularExp;
        
        varying vec3 vNormal;
        varying vec3 vPosition;

        void main(void) {

            const float PI = 3.14159265358979323846264;
        
            vec3 light = uLightPos - vPosition;
            float distance = length(light);
            light = normalize(light);
            
            vec3 normal = normalize(vNormal);
            vec3 view = normalize(-vPosition);
            vec3 halfV = normalize(view+light);
                
            float NL = max(0.0, dot(normal, light));
            float NH = max(0.0, dot(normal, halfV));
            float NHexp = pow(NH, uSpecularExp);
                    
            float attenuation = 1.0 / ( 4.0 * PI * distance );
            float lightI = uLightIntensity * attenuation;
            
            vec3 color = uAmbient + (uDiffuse * NL + uSpecular * NHexp) * lightI;
            gl_FragColor = vec4(color, 1.0);
        }
    </script>

    <script id="shader-vs-blinn" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;
        
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;
        uniform mat3 uNMatrix;
        
        varying vec3 vPosition;
        varying vec3 vNormal;

        void main(void) {
        
            vNormal = uNMatrix * aVertexNormal;
            vPosition = vec3(uMVMatrix * vec4(aVertexPosition, 1.0));
            
            gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        }
    </script>

    <script id="shader-fs-storm" type="x-shader/x-fragment">


        precision mediump float;
        uniform float iGlobalTime;

        varying vec2 vTextureCoord;
        
        float rand(vec2 p){
        p+=.2127+p.x+.3713*p.y;
        vec2 r=4.789*sin(789.123*(p));
        return fract(r.x*r.y);
        }

        float sn(vec2 p){
            vec2 i=floor(p-.5);
            vec2 f=fract(p-.5);
            f = f*f*f*(f*(f*6.0-15.0)+10.0);
            float rt=mix(rand(i),rand(i+vec2(1.,0.)),f.x);
            float rb=mix(rand(i+vec2(0.,1.)),rand(i+vec2(1.,1.)),f.x);
            return mix(rt,rb,f.y);
        }

        void main( void )
        {

            //vec2 iMouse(0, 0);

            vec2 iResolution = vec2(800, 500);
            vec2 fragCoord = gl_FragCoord.xy;


            vec4 fragColor;


            //vec2 uv = fragCoord.xy / iResolution.y;
            vec2 uv = vTextureCoord.xy / 0.5;
            
            vec2 p=uv.xy*vec2(3.,4.3);
            float f =
            .5*sn(p)
            +.25*sn(2.*p)
            +.125*sn(4.*p)
            +.0625*sn(8.*p)
            +.03125*sn(16.*p)+
            .015*sn(32.*p)
            ;
            
            float newT = iGlobalTime*0.4 + sn(vec2(iGlobalTime*1.))*0.1;
            p.x-=iGlobalTime*0.2;
            
            p.y*=1.3;
            float f2=
            .5*sn(p)
            +.25*sn(2.04*p+newT*1.1)
            -.125*sn(4.03*p-iGlobalTime*0.3)
            +.0625*sn(8.02*p-iGlobalTime*0.4)
            +.03125*sn(16.01*p+iGlobalTime*0.5)+
            .018*sn(24.02*p);
            
            float f3=
            .5*sn(p)
            +.25*sn(2.04*p+newT*1.1)
            -.125*sn(4.03*p-iGlobalTime*0.3)
            +.0625*sn(8.02*p-iGlobalTime*0.5)
            +.03125*sn(16.01*p+iGlobalTime*0.6)+
            .019*sn(18.02*p);
            
            float f4 = f2*smoothstep(0.0,1.,uv.y);
            
            vec3 clouds = mix(vec3(-0.4,-0.4,-0.15),vec3(1.4,1.4,1.3),f4*f);
            float lightning = sn((f3)+vec2(pow(sn(vec2(iGlobalTime*4.5)),6.)));

            lightning *= smoothstep(0.0,1.,uv.y+0.5);

            lightning = smoothstep(0.76,1.,lightning);
            lightning=lightning*2.;
            
            vec2 moonp = vec2(0.5,0.8);
            float moon = smoothstep(0.95,0.956,1.-length(uv-moonp));
            vec2 moonp2 = moonp + vec2(0.015, 0);
            moon -= smoothstep(0.93,0.956,1.-length(uv-moonp2));
            moon = clamp(moon, 0., 1.);
            moon += 0.3*smoothstep(0.80,0.956,1.-length(uv-moonp));

            clouds+= pow(1.-length(uv-moonp),1.7)*0.3;

            clouds*=0.8;
            clouds += lightning + moon +0.2;

            float ground = smoothstep(0.07,0.075,f*(p.y-0.98)+0.01);
            
            vec2 newUV = uv;
            newUV.x-=iGlobalTime*0.3;
            newUV.y+=iGlobalTime*3.;
            float strength = sin(iGlobalTime*0.5+sn(newUV))*0.1+0.15;
            
            float rain = sn( vec2(newUV.x*20.1, newUV.y*40.1+newUV.x*400.1-20.*strength ));
            float rain2 = sn( vec2(newUV.x*45.+iGlobalTime*0.5, newUV.y*30.1+newUV.x*200.1 ));  
            rain = strength-rain;
            rain+=smoothstep(0.2,0.5,f4+lightning+0.1)*rain;
            rain += pow(length(uv-moonp),1.)*0.1;
            rain+=rain2*(sin(strength)-0.4)*2.;
            rain = clamp(rain, 0.,0.5)*0.5;
            
            float tree = 0.;
            vec2 treeUV = uv;

            {
                float atree = abs(atan(treeUV.x*59.-85.5,3.-treeUV.y*25.+5.));
                atree +=rand(treeUV.xy*vec2(0.001,1.))*atree;
                tree += clamp(atree, 0.,1.)*0.33;
            }
            {
                float atree = abs(atan(treeUV.x*65.-65.5,3.-treeUV.y*19.+4.));
                atree +=rand(treeUV.xy*vec2(0.001,1.))*atree;
                tree += clamp(atree, 0.,1.)*0.33;
            }
            {
                float atree = abs(atan(treeUV.x*85.-91.5,3.-treeUV.y*21.+4.));
                atree +=rand(treeUV.xy*vec2(0.001,1.))*atree;
                tree += clamp(atree, 0.,1.)*0.34;
            }
            tree = smoothstep(0.6,1.,tree);
            
            
            vec3 painting = tree*ground*(clouds + rain)+clamp(rain*(strength-0.1),0.,1.);
            
            float r=1.-length(max(abs(fragCoord.xy / iResolution.xy*2.-1.)-.5,0.)); 
            painting*=r;
            
            fragColor = vec4(painting, 1.0);

            gl_FragColor = fragColor;

            //gl_FragColor = vec4(vTextureCoord.x, vTextureCoord.y, 0.0,1.0);
        }

    </script>

    <script id="shader-fs-ball-light" type="x-shader/x-fragment">


        precision mediump float;


        // Yuldashev Mahmud Effect took from shaderToy mahmud9935@gmail.com


        uniform float iGlobalTime;
        float time = iGlobalTime;
        vec2 resolution = vec2(700,700);

        float snoise(vec3 uv, float res)
        {
            const vec3 s = vec3(1e0, 1e2, 1e3);
            
            uv *= res;
            
            vec3 uv0 = floor(mod(uv, res))*s;
            vec3 uv1 = floor(mod(uv+vec3(1.), res))*s;
            
            vec3 f = fract(uv); f = f*f*(3.0-2.0*f);

            vec4 v = vec4(uv0.x+uv0.y+uv0.z, uv1.x+uv0.y+uv0.z,
                          uv0.x+uv1.y+uv0.z, uv1.x+uv1.y+uv0.z);

            vec4 r = fract(sin(v*1e-1)*1e3);
            float r0 = mix(mix(r.x, r.y, f.x), mix(r.z, r.w, f.x), f.y);
            
            r = fract(sin((v + uv1.z - uv0.z)*1e-1)*1e3);
            float r1 = mix(mix(r.x, r.y, f.x), mix(r.z, r.w, f.x), f.y);
            
            return mix(r0, r1, f.z)*2.-1.;
        }

        void main( void ) {

            vec2 p = -.5 + gl_FragCoord.xy / resolution.xy;
            p.x *= resolution.x/resolution.y;
            
            float color = 3.0 - (3.*length(2.*p));
            
            vec3 coord = vec3(atan(p.x,p.y)/6.2832+.5, length(p)*.4, .5);
            
            for(int i = 1; i <= 7; i++)
            {
                float power = pow(2.0, float(i));
                color += (1.5 / power) * snoise(coord + vec3(0.,-time*.05, time*.01), power*16.);
            }
            //lila
            gl_FragColor = vec4(color, pow(max(color,0.),3.)*0.15 , pow(max(color,0.),2.)*0.4, .6);//rosa
            gl_FragColor = vec4( pow(max(color,0.),2.)*0.4,  color,pow(max(color,0.),3.)*0.15, .6);
        }
    </script>

    <script id="glow-shader-fs" type="x-shader/x-fragment">
        precision mediump float;

        // Uniforms
        uniform sampler2D uSamplerGlow;

        // Varyings
        varying vec2 vTextureCoord;

        // Renderitza utilitzant el mapa de "glow"
        void main ()
        {
            gl_FragColor = vec4 (texture2D(uSamplerGlow, vTextureCoord).rgb, 1.0);
        }
    </script>

    <script id="glow-shader-vs" type="x-shader/x-vertex">
        attribute vec3 aVertexPosition;
        attribute vec2 aTextureCoord;
        
        uniform mat4 uMVMatrix;
        uniform mat4 uPMatrix;

        varying vec2 vTextureCoord;
        
        void main(void) {
        
            vTextureCoord = aTextureCoord;          
            gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        }
    </script>

    <script id="blur-shader-fs" type="x-shader/x-fragment">
        precision mediump float;

        // Uniforms
        uniform int uIsHorizontal;
        uniform vec2 uTexelSize;
        uniform sampler2D uSamplerBlur;
        uniform float intensity;

        // Varyings
        varying vec2 vTextureCoord;

        // Valor de la gaussiana amb desviació típica "deviation" en la posició "x"
        float Gaussian (float x, float deviation)
        {
            return (1.0 / sqrt(2.0 * 3.141592 * deviation)) * exp(-((x * x) / (2.0 * deviation)));  
        }

        void main ()
        {
            const int BlurAmount = 12;
            //const float intensity = 1.2;
            float halfBlur = float(BlurAmount) * 0.5;
            
            // Gaussian deviation
            float deviation = halfBlur * 0.35;
            deviation *= deviation;

            vec4 color = vec4(0.0);
            
            if (uIsHorizontal == 1) {
                // Blur horitzontal
                for (int i = 0; i < BlurAmount; ++i)
                {
                    float offset = float(i) - halfBlur;
                    color += texture2D(uSamplerBlur, vTextureCoord + vec2(offset * uTexelSize.x, 0.0)) * Gaussian(offset, deviation) * intensity;
                }
            } else {
                // Blur vertical
                for (int i = 0; i < BlurAmount; ++i)
                {
                    float offset = float(i) - halfBlur;
                    color += texture2D(uSamplerBlur, vTextureCoord + vec2(0.0, offset * uTexelSize.y)) * Gaussian(offset, deviation) * intensity;
                }
            }

            // Aplica el color de sortida
            gl_FragColor = clamp(color, 0.0, 1.0);
        }
    </script>

    <script id="blur-shader-vs" type="x-shader/x-vertex">
        attribute vec2 aVertexPosition;
        attribute vec2 aTextureCoord;
        
        varying vec2 vTextureCoord;
        
        void main(void) {
        
            vTextureCoord = aTextureCoord;          
            gl_Position = vec4(aVertexPosition, 0.0, 1.0);
        }
    </script>

</div>

</body>

</html>